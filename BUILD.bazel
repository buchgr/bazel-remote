load("@bazel_gazelle//:def.bzl", "gazelle")
load("@io_bazel_rules_docker//container:container.bzl", "container_image", "container_push")
load("@io_bazel_rules_docker//contrib:passwd.bzl", "passwd_entry", "passwd_tar")
load("@io_bazel_rules_docker//go:image.bzl", "go_image")
load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

gazelle(
    name = "gazelle",
    prefix = "github.com/buchgr/bazel-remote",
)

go_library(
    name = "go_default_library",
    srcs = ["main.go"],
    importpath = "github.com/buchgr/bazel-remote",
    visibility = ["//visibility:private"],
    deps = [
        "//cache:go_default_library",
        "//cache/disk:go_default_library",
        "//cache/gcsproxy:go_default_library",
        "//cache/httpproxy:go_default_library",
        "//cache/s3proxy:go_default_library",
        "//config:go_default_library",
        "//server:go_default_library",
        "//utils/idle:go_default_library",
        "//utils/rlimit:go_default_library",
        "@com_github_abbot_go_http_auth//:go_default_library",
        "@com_github_grpc_ecosystem_go_grpc_prometheus//:go_default_library",
        "@com_github_prometheus_client_golang//prometheus/promhttp:go_default_library",
        "@com_github_slok_go_http_metrics//metrics/prometheus:go_default_library",
        "@com_github_slok_go_http_metrics//middleware:go_default_library",
        "@com_github_urfave_cli_v2//:go_default_library",
        "@org_golang_google_grpc//:go_default_library",
        "@org_golang_google_grpc//credentials:go_default_library",
    ],
)

go_binary(
    name = "bazel-remote",
    embed = [":go_default_library"],
    pure = "on",
    static = "on",
    visibility = ["//visibility:public"],
    x_defs = {"main.gitCommit": "{STABLE_GIT_COMMIT}"},
)

# Create the non-root user, "bazel-remote", so the container can run with reduced
# privileges.
BAZEL_REMOTE_USER_NAME = "bazel-remote"
BAZEL_REMOTE_USER_ID = 1000
passwd_entry(
    name = "bazelremote_passwd_entry",
    username = BAZEL_REMOTE_USER_NAME,
    uid = BAZEL_REMOTE_USER_ID,
    gid = BAZEL_REMOTE_USER_ID,
    home = "/home/" + BAZEL_REMOTE_USER_NAME,
    create_home = True,
)

passwd_tar(
    name = "passwd",
    entries = [":bazelremote_passwd_entry"],
    passwd_file_pkg_dir = "/etc",
)

go_image(
    name = "bazel-remote-base",
    embed = [":go_default_library"],
    goarch = "amd64",
    goos = "linux",
    pure = "on",
    static = "on",
    visibility = ["//visibility:private"],
    x_defs = {"main.gitCommit": "{STABLE_GIT_COMMIT}"},
)

go_image(
    name = "bazel-remote-base-arm64",
    embed = [":go_default_library"],
    goarch = "arm64",
    goos = "linux",
    pure = "on",
    static = "on",
    visibility = ["//visibility:private"],
    x_defs = {"main.gitCommit": "{STABLE_GIT_COMMIT}"},
)

# Initialize the default /data storage directory since the bazel-remote
# user does not have sufficient permissions at the root (/) directory.
# Since an empty directory cannot be packaged by the pkg_tar rule, create
# the "EMPTY" file.
genrule(
    name = "empty",
    srcs = [],
    outs = ["EMPTY"],
    cmd = "touch $@",
)

pkg_tar(
    name = "data_tar",
    srcs = [":empty"],
    package_dir = "/data",
    owner = "{0}.{0}".format(BAZEL_REMOTE_USER_ID),
)

container_image(
    name = "bazel-remote-image",
    base = ":bazel-remote-base",
    cmd = ["--max_size=5"],
    entrypoint = [
        "/app/bazel-remote-base.binary",
        "--port=8080",
        "--dir=/data",

        # Listen on all addresses, not just 127.0.0.1, so this can
        # be reached from outside the container (with a -p mapping).
        "--profile_host=",
        # Specify a port to enable the profiling http server.
        "--profile_port=6060",
    ],
    ports = ["8080"],
    tars = [
        ":passwd",
        ":data_tar",
    ],
    user = str(BAZEL_REMOTE_USER_ID),
    visibility = ["//visibility:public"],
)

container_image(
    name = "bazel-remote-image-arm64",
    base = ":bazel-remote-base-arm64",
    cmd = ["--max_size=1"],
    entrypoint = [
        "/app/bazel-remote-base-arm64.binary",
        "--port=8080",
        "--dir=/data",

        # Listen on all addresses, not just 127.0.0.1, so this can
        # be reached from outside the container (with a -p mapping).
        "--profile_host=",
        # Specify a port to enable the profiling http server.
        "--profile_port=6060",
    ],
    ports = ["8080"],
    tars = [
        ":passwd",
        ":data_tar",
    ],
    user = str(BAZEL_REMOTE_USER_ID),
    visibility = ["//visibility:public"],
)

container_push(
    name = "push_to_dockerhub",
    format = "Docker",
    image = ":bazel-remote-image",
    registry = "index.docker.io",
    repository = "buchgr/bazel-remote-cache",
    tag = "latest",
    visibility = ["//visibility:public"],
)
